buildscript {
  repositories {
    jcenter()
    maven { url 'https://plugins.gradle.org/m2' }
  }
  dependencies {
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    classpath 'de.undercouch:gradle-download-task:3.1.1'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
  }
}

apply from: 'build.gradle'
apply plugin: 'signing'

defaultTasks 'clean', 'check', 'allTests', 'install', 'uploadArchives'

signing {
  sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: project.version.endsWith("-SNAPSHOT") ?
                            'https://oss.sonatype.org/content/repositories/snapshots/' :
                            'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
              authentication(userName: "$sonatypeUsername", password: "$sonatypePassword")
            }

            pom.project {
              name archivesBaseName
              packaging 'jar'
              description 'Selenide = concise API for Selenium WebDriver'
              url 'https://github.com/codeborne/selenide'

               scm {
                   url 'scm:git@github.com:codeborne/selenide.git'
                   connection 'scm:git@github.com:codeborne/selenide.git'
                   developerConnection 'scm:git@github.com:codeborne/selenide.git'
               }

               licenses {
                   license {
                       name 'MIT'
                       url 'https://opensource.org/licenses/MIT'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'asolntsev'
                       name 'Andrei Solntsev'
                   }
                   developer {
                       id 'vadger'
                       name 'Vadim Gerasimov'
                   }
                   developer {
                       id 'vinogradoff'
                       name 'Aleksei Vinogradov'
                   }
                   developer {
                       id 'angryziber'
                       name 'Anton Keks'
                   }
               }
           }

           //mess with the generated pom to set the 'packaging' tag
           pom.withXml { XmlProvider xmlProvider ->
               def xml = xmlProvider.asString()
               def pomXml = new XmlParser().parse(new ByteArrayInputStream(xml.toString().bytes))

               pomXml.version[0] + { packaging('jar') }

               def newXml = new StringWriter()
               def printer = new XmlNodePrinter(new PrintWriter(newXml))
               printer.preserveWhitespace = true
               printer.print(pomXml)
               xml.setLength(0)
               xml.append(newXml.toString())
           }
        }
    }
}
