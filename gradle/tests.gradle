subprojects {
  test {
    include 'com/codeborne/selenide/**/*'
  }

  task ie(type: Test) {
    systemProperties['selenide.browser'] = 'ie'
    systemProperties['selenide.timeout'] = '8000'
    include 'integration/**/*'
    exclude '**/AlertText.*'
    exclude '**/ConfirmTest.*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task edge(type: Test) {
    systemProperties['selenide.browser'] = 'edge'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task edge_headless(type: Test) {
    systemProperties['selenide.browser'] = 'edge'
    systemProperties['selenide.headless'] = 'true'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task chrome(type: Test) {
    systemProperties['selenide.browser'] = 'chrome'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task chrome_headless(type: Test) {
    systemProperties['selenide.browser'] = 'chrome'
    systemProperties['selenide.headless'] = 'true'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task chrome_remote(type: Test) {
    systemProperties['selenide.remote'] = 'http://localhost:4444/wd/hub'
    systemProperties['selenide.browser'] = 'chrome'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task firefox(type: Test) {
    systemProperties['selenide.browser'] = 'firefox'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task firefox_headless(type: Test) {
    systemProperties['selenide.browser'] = 'firefox'
    systemProperties['selenide.headless'] = 'true'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  task safari(type: Test) {
    systemProperties['selenide.browser'] = 'safari'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    maxParallelForks = 1
    outputs.upToDateWhen {false}
  }

  task firefox_remote(type: Test) {
    systemProperties['selenide.remote'] = 'http://localhost:4444/wd/hub'
    systemProperties['selenide.browser'] = 'firefox'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    outputs.upToDateWhen {false}
  }

  tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties['BUILD_URL'] = System.getenv()['BUILD_URL']
    systemProperties['selenide.reportsFolder'] = "${reportsDir}/tests/${name}"
    systemProperties['selenide.downloadsFolder'] = "${buildDir}/tests/downloads"

    System.properties.stringPropertyNames()
      .findAll { it.startsWith("selenide.") }
      .forEach {
        println " set ${it} to ${System.getProperty(it)}"
        systemProperties[it] = System.getProperty(it)
      }

    if (System.getenv('CI')) {
      maxParallelForks = 1
    } else {
      maxParallelForks = (int) Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
    jvmArgs = ['-ea', '-Xmx512m']
    systemProperties['file.encoding'] = encoding
    systemProperties['user.country'] = 'TR'
    systemProperties['user.language'] = 'tr'
  }
}

task allTests(dependsOn: ['clean', 'check', 'firefox_headless', 'chrome_headless']) {}
