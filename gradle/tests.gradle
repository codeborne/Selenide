subprojects {
  test {
    include 'com/codeborne/selenide/**/*'
  }

  task ie(type: Test) {
    systemProperties['selenide.browser'] = 'ie'
    systemProperties['selenide.timeout'] = '8000'
    include 'integration/**/*'
    exclude '**/AlertText.*'
    exclude '**/ConfirmTest.*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task edge(type: Test) {
    systemProperties['selenide.browser'] = 'edge'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task edge_headless(type: Test) {
    systemProperties['selenide.browser'] = 'edge'
    systemProperties['selenide.headless'] = 'true'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task chrome(type: Test) {
    systemProperties['selenide.browser'] = 'chrome'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task chrome_headless(type: Test) {
    systemProperties['selenide.browser'] = 'chrome'
    systemProperties['selenide.headless'] = 'true'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task chrome_remote(type: Test) {
    systemProperties['selenide.remote'] = 'http://localhost:4444/wd/hub'
    systemProperties['selenide.browser'] = 'chrome'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task firefox(type: Test) {
    systemProperties['selenide.browser'] = 'firefox'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task firefox_headless(type: Test) {
    systemProperties['selenide.browser'] = 'firefox'
    systemProperties['selenide.headless'] = 'true'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task safari(type: Test) {
    systemProperties['selenide.browser'] = 'safari'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
    maxParallelForks = 1
  }

  task opera(type: Test) {
    systemProperties['selenide.browser'] = 'opera'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task opera_headless(type: Test) {
    systemProperties['selenide.browser'] = 'opera'
    systemProperties['selenide.headless'] = 'true'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task firefox_remote(type: Test) {
    systemProperties['selenide.remote'] = 'http://localhost:4444/wd/hub'
    systemProperties['selenide.browser'] = 'firefox'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty('file.encoding', encoding)
    systemProperty('BUILD_URL', System.getenv()['BUILD_URL'])
    systemProperties['selenide.reportsFolder'] = "${reportsDir}/tests/${name}"
    systemProperties['selenide.downloadsFolder'] = "${buildDir}/tests/downloads"

    if (System.getenv('CI')) {
      maxParallelForks = 1
    } else {
      maxParallelForks = (int) Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
    jvmArgs = ['-ea', '-Xmx512m']
    systemProperties += System.properties
    systemProperties['user.country'] = 'TR'
    systemProperties['user.language'] = 'tr'
  }
}

task allTests(dependsOn: ['clean', 'check', 'firefox_headless', 'chrome_headless']) {}
